plugins {
    id 'org.mozilla.rust-android-gradle.rust-android'
    id 'com.android.library'
    id 'maven-publish'
    alias(libs.plugins.jetbrains.kotlin.android)
}

android {
    namespace 'network.beechat.kaonic'
    compileSdk 35
    ndkVersion '29.0.13113456'

    defaultConfig {
        minSdk 24
        targetSdk 35
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        externalNativeBuild {
            cmake {
                def gstRoot

                if (project.hasProperty('gstAndroidRoot'))
                    gstRoot = project.gstAndroidRoot
                else
                    gstRoot = System.env.GSTREAMER_ROOT_ANDROID

                if (gstRoot == null)
                    throw new GradleException('GSTREAMER_ROOT_ANDROID must be set, or "gstAndroidRoot" must be defined in your gradle.properties in the top level directory of the unpacked universal GStreamer Android binaries')

                arguments "-DANDROID_STL=c++_shared", "-DGSTREAMER_ROOT_ANDROID=$gstRoot", "-GNinja"

                targets "kaonic-streamer"

                // All archs except MIPS and MIPS64 are supported
                abiFilters  'armeabi-v7a', 'arm64-v8a'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    externalNativeBuild {
        cmake {
            path 'jni/CMakeLists.txt'
        }
    }

    buildFeatures {
        aidl true
    }

//    sourceSets {
//        main {
//            jniLibs.srcDirs = ['gstreamer-1.0-android-arm64/lib']
//        }
//    }

//    packagingOptions {
//        pickFirst '**/*.so'
//    }
}

afterEvaluate {
    if (project.hasProperty('compileDebugJavaWithJavac'))
        project.compileDebugJavaWithJavac.dependsOn 'externalNativeBuildDebug'
    if (project.hasProperty('compileReleaseJavaWithJavac'))
        project.compileReleaseJavaWithJavac.dependsOn 'externalNativeBuildRelease'
}

cargo {
    module = "./kaonic"
    libname = "kaonic"
    targets = ["arm", "arm64", "x86"]
    verbose
    profile = 'release'
    features {
        defaultAnd("android")
    }
}

project.afterEvaluate {
    tasks.withType(com.nishtahir.CargoBuildTask).each { buildTask ->
        tasks.withType(com.android.build.gradle.tasks.MergeSourceSetFolders).configureEach { mergeTask ->
            mergeTask.inputs.dir(
                    layout.buildDirectory.dir("rustJniLibs" + File.separator + buildTask.toolchain.folder)
            )
            mergeTask.dependsOn(buildTask)
        }
    }
}

dependencies {
    implementation libs.androidx.core.ktx
    implementation libs.androidx.appcompat
    implementation libs.material
    implementation libs.core.ktx
    implementation libs.usb.serial.for1.android
    implementation libs.jackson.dataformat.cbor
    implementation libs.androidx.security.crypto

    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core
}
