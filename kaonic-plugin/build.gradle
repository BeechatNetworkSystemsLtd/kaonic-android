plugins {
    id 'org.mozilla.rust-android-gradle.rust-android'
    id 'com.android.library'
    id 'maven-publish'
    alias(libs.plugins.jetbrains.kotlin.android)
}

android {
    namespace 'network.beechat.kaonic'
    compileSdk 35
    ndkVersion '29.0.13113456'

    defaultConfig {
        minSdk 24
        targetSdk 35
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

cargo {
    module = "./kaonic"
    libname = "kaonic"
    targets = ["arm", "arm64", "x86"]
    verbose
    profile = 'release'
    features {
        defaultAnd("android")
    }
}

project.afterEvaluate {
    tasks.withType(com.nishtahir.CargoBuildTask).each { buildTask ->
        tasks.withType(com.android.build.gradle.tasks.MergeSourceSetFolders).configureEach { mergeTask ->
            mergeTask.inputs.dir(
                    layout.buildDirectory.dir("rustJniLibs" + File.separator + buildTask.toolchain.folder)
            )
            mergeTask.dependsOn(buildTask)
        }
    }
}

dependencies {
    implementation libs.androidx.core.ktx
    implementation libs.androidx.appcompat
    implementation libs.material
    implementation libs.core.ktx
    implementation libs.usb.serial.for1.android
    implementation libs.jackson.dataformat.cbor
    implementation libs.androidx.security.crypto

    implementation "androidx.camera:camera-core:1.3.1"
    implementation "androidx.camera:camera-camera2:1.3.1"
    implementation "androidx.camera:camera-lifecycle:1.3.1"
    implementation "androidx.camera:camera-view:1.3.1"

    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core
}
